plugins {
    id 'java'
}

group = 'com.agussumaryanto'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.seleniumhq.selenium:selenium-java:4.35.0'
    testImplementation 'io.github.bonigarcia:webdrivermanager:6.3.1'

    testImplementation 'io.cucumber:cucumber-java:7.27.2'
    testImplementation 'io.cucumber:cucumber-junit:7.27.2'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.28.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.13.4'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.13.4'
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.13.4' // allow JUnit4 tests on Gradle 9
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    implementation("org.slf4j:slf4j-api:2.0.17") // Add a logger backend to your testRuntimeOnly
    implementation("ch.qos.logback:logback-classic:1.5.18") // Add a logger backend to your testRuntimeOnly
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

configurations {
    cucumberRuntime {
        extendsFrom(testImplementation, testRuntimeOnly)
    }
}

def tags = (findProperty('tags') == null) ? 'not @exclude' : findProperty('tags') + ' and not @exclude'

tasks.register("cucumber", JavaExec) { // >>> using tasks.register for Gradle 9
    description = "Run Cucumber features"
    group = "verification"

    dependsOn(tasks.testClasses)

    mainClass = "io.cucumber.core.cli.Main"
    classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output

    args = [
            "--plugin", "pretty",
            "--plugin", "html:reports/cucumber.html",
            "--glue", "saucedemo.stepdef",
            "--tags", tags,
            "src/test/resources"
    ]
}